apply plugin: 'com.android.application'
apply plugin: 'com.droidtitan.lintcleaner'

android {
    compileSdkVersion 20
    buildToolsVersion "19.1.0"

    defaultConfig {
        applicationId "io.seal.swarmwear"
        minSdkVersion 18
        targetSdkVersion 20
        versionCode 20
        versionName "1.14"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            resValue "string", "app_name", "Wear for Swarm Debug"
            versionNameSuffix " Debug"
            applicationIdSuffix ".debug"
        }

        release {
            resValue "string", "app_name", "Wear for Swarm"
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile

                    def fileName = file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + ".apk")
                    output.outputFile = new File(file.parent, fileName)
                }
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    lintOptions {
        abortOnError false
    }
}

if (project.hasProperty('signingStoreLocation')) {
    android.signingConfigs.release.storeFile = file(signingStoreLocation)
}
if (project.hasProperty('signingStorePassword')) {
    android.signingConfigs.release.storePassword = signingStorePassword
}
if (project.hasProperty('signingKeyPassword')) {
    android.signingConfigs.release.keyPassword = signingKeyPassword
}
if (project.hasProperty('signingKeyAlias')) {
    android.signingConfigs.release.keyAlias = signingKeyAlias
}

def type = "String"
def crittercismConstantName = "CRITTERCISM_APP_ID"
def gaConstantName = "GOOGLE_ANALYTICS_PROPERTY_ID"
def foursquareClientIdConstantName = "FOURSQUARE_CLIENT_ID"
def foursquareClientSecretConstantName = "FOURSQUARE_CLIENT_SECRET"

project.ext.crittercismId = '"' + (project.hasProperty('crittercismAppId') ? crittercismAppId: "") + '"'
project.ext.gaId = '"' + (project.hasProperty('gaPropertyId') ?  gaPropertyId : "") + '"'
project.ext.fsClientId = '"' + (project.hasProperty('foursquareClientId') ?  foursquareClientId : "") + '"'
project.ext.fsClientSecret = '"' + (project.hasProperty('foursquareClientSecret') ?  foursquareClientSecret : "") + '"'

android.buildTypes.debug.buildConfigField type, crittercismConstantName,  crittercismId
android.buildTypes.debug.buildConfigField type, gaConstantName, gaId
android.buildTypes.debug.buildConfigField type, foursquareClientIdConstantName, fsClientId
android.buildTypes.debug.buildConfigField type, foursquareClientSecretConstantName, fsClientSecret

android.buildTypes.release.buildConfigField type, crittercismConstantName, crittercismId
android.buildTypes.release.buildConfigField type, gaConstantName, gaId
android.buildTypes.release.buildConfigField type, foursquareClientIdConstantName, fsClientId
android.buildTypes.release.buildConfigField type, foursquareClientSecretConstantName, fsClientSecret

configurations {
    all*.exclude group: 'org.apache.commons'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:support-v13:20.0.0"
    compile 'com.octo.android.robospice:robospice:1.4.12'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.12'
    compile 'com.squareup.retrofit:retrofit:1.7.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.github.johnkil.android-progressfragment:progressfragment-native:1.4.0'
    compile 'commons-io:commons-io:2.4'
    compile 'com.foursquare:foursquare-android-nativeoauth-lib:1.0.0'
    compile 'fr.nicolaspomepuy.androidwearcrashreport:crashreport-mobile:0.4@aar'
    compile project(":shared")
    wearApp project(':wearable')
}